/**
 * Typed Eva: static typecheker.
 *
 * (C) 2022-present Dmitry Soshnikov <dmitry.soshnikov@gmail.com>
 */

/**
 * S-expression parser.
 *
 * Atom: 42, foo, bar, "Hello World"
 *
 * List: (), (+ 5 x), (print "hello")
 */

// -----------------------------------------------
// Lexical grammar (tokens):


%lex

%%

\/\/.*            /* skip comments */
\/\*[\s\S]*?\*\/  /* skip comments */

\s+               /* skip whitespace */

\"[^\"]*\"        return 'STRING'

\d+               return 'NUMBER'

[\w\-+*=<>/:,!]+  return 'SYMBOL'

/lex


// -----------------------------------------------
// Syntactic grammar (BNF):

%%

Exp
  : Atom
  | List
  ;

Atom
  : NUMBER { $$ = Number($1) }
  | STRING
  | SYMBOL
  ;

List
  : '(' ListEntries ')' { $$ = $2 }
  ;

ListEntries
  : ListEntries Exp { $1.push($2); $$ = $1 }
  | /* empty */     { $$ = [] }
  ;














